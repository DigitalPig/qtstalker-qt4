TEMPLATE = lib

!win32 {
	CONFIG += thread warn_on plugin
} else {
        CONFIG += thread warn_on plugin debug_and_release
        CONFIG(debug, debug|release): DESTDIR = ../../../mingwin32/deploydebug
        CONFIG(release, debug|release): DESTDIR = ../../../mingwin32/deploy
        # we need to include headers for moccing of library otherwise, the
        # loading at runtime of library will fail
        HEADERS += ../../../lib/quoteplugin.h
        DEFINES += MINGW
}

CONFIG(debug, debug|release): DEFINES += DEBUG _DEBUG

QMAKE_CXXFLAGS += -ffast-math
#QMAKE_CXXFLAGS += -Os

TARGET = $${pluginName}.$${PG_VERSION_PLUGIN}

win32 {
	CONFIG(debug, debug|release): TARGET = $${pluginName}.$${PG_VERSION_PLUGIN}D
}

INCLUDEPATH += ../../../lib

win32 {
    INCLUDEPATH += ../../../mingwin32/ta-lib/include
    INCLUDEPATH += ../../../mingwin32/db/include
    CONFIG(debug, debug|release): LIBS += -L../../../mingwin32/deploydebug -lqtstalkerD0
    CONFIG(release, debug|release): LIBS += -L../../../mingwin32/deploy -lqtstalker0

} else {
	LIBS += -L../../../lib -lqtstalker
}

